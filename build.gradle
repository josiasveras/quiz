plugins {
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'groovy'
    id 'jacoco'
}

group = 'com.app'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    // Web and REST API
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Persistence and JPA support
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Data validation
    implementation("org.springframework.boot:spring-boot-starter-validation")

    // Groovy language support
    implementation 'org.apache.groovy:groovy:4.0.14'

    // Unit testing with Core Spring Boot and Spock Framework
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'
    testImplementation("org.spockframework:spock-core:2.4-M6-groovy-4.0")
}

springBoot {
    mainClass = 'com.app.quiz.QuizApiApplication'
}

jacoco {
    toolVersion = "0.8.11"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
    }
}